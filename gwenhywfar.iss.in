; Script generated by the Inno Setup Script Wizard and heavily
; modified by hand by Christian Stimming.
;
; Inno Setup Compiler: See http://www.jrsoftware.org/isdl.php
;

[Setup]
; Using the name here directly because we want it capitalized
AppName=Gwenhywfar
AppVerName=Gwenhywfar @VERSION@
AppPublisher=Gwenhywfar Development Team
AppPublisherURL=http://sourceforge.net/projects/gwenhywfar
AppSupportURL=http://sourceforge.net/support/getsupport.php?group_id=88769
AppUpdatesURL=http://sourceforge.net/project/showfiles.php?group_id=88769
DefaultDirName={pf}\@PACKAGE@
LicenseFile=COPYING
Compression=lzma
SolidCompression=yes
OutputDir=.
OutputBaseFilename=@PACKAGE@-@VERSION@-setup
UninstallFilesDir={app}\uninstall\@PACKAGE@
InfoAfterFile=README

[Types]
Name: "full"; Description: "{cm:FullInstall}"
Name: "binary"; Description: "{cm:BinaryInstall}"
Name: "custom"; Description: "{cm:CustomInstall}"; Flags: iscustom

[Components]
Name: "main"; Description: "{cm:MainFiles}"; Types: full binary custom; Flags: fixed
Name: "devel"; Description: "{cm:DevelFiles}"; Types: full
Name: "doc"; Description: "{cm:DocFiles}"; Types: full

[Files]
; The main DLL
Source: "src\@PACKAGE@32_@GWENHYWFAR_SO_EFFECTIVE@.dll"; DestDir: "{win}"; Flags: sharedfile; Components: main

; The lib file for linking against it
Source: "src\@PACKAGE@32_@GWENHYWFAR_SO_EFFECTIVE@.lib"; DestDir: "{app}\lib"; Flags: sharedfile; Components: devel

; The config-script, the headers, and the aclocal macro. FIXME: Are
; there additional libraries in $prefix/lib which should be
; distributed? So far they are not distributed.
Source: "distribution\win32-tmp\bin\*"; DestDir: "{app}\bin"; Flags: recursesubdirs; Components: devel
Source: "distribution\win32-tmp\include\*"; DestDir: "{app}\include"; Flags: ignoreversion recursesubdirs; Components: devel
Source: "distribution\win32-tmp\share\*"; DestDir: "{app}\share"; Flags: ignoreversion recursesubdirs; Components: devel

; And all the documentation
Source: "README"; DestDir: "{app}\doc\@PACKAGE@"; Components: doc
Source: "COPYING"; DestDir: "{app}\doc\@PACKAGE@"; Flags: ignoreversion; Components: doc
Source: "README.W32"; DestDir: "{app}\doc\@PACKAGE@"; Components: doc
Source: "AUTHORS"; DestDir: "{app}\doc\@PACKAGE@"; Components: doc
Source: "ChangeLog"; DestDir: "{app}\doc\@PACKAGE@"; Components: doc


[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "de"; MessagesFile: "compiler:Languages\German.isl"
Name: "pt"; MessagesFile: "compiler:Languages\PortugueseStd.isl"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"

[Messages]
; *** "Select Destination" wizard page
SelectDirLabel3=Setup will install [name] into the following folder. If you have MinGW installed on your system, you can safely choose MinGW's root folder here as well (e.g. c:\msys\1.0).

de.SelectDirLabel3=Das Setup wird [name] in den folgenden Ordner installieren. Wenn Sie MinGW auf Ihrem Computer installiert haben, können Sie hier problemlos das Root-Verzeichnis von MinGW angeben (z.B. c:\msys\1.0).


[CustomMessages]
; *** "Select Components" wizard page
FullInstall=Full installation
BinaryInstall=Install only libraries
CustomInstall=Custom installation

MainFiles=Gwenhywfar DLL Library
DevelFiles=Development files
DocFiles=Documentation files

de.FullInstall=Komplett-Installation
de.BinaryInstall=Nur Bibliotheken installieren
de.CustomInstall=Benutzerdefiniert

de.MainFiles=Gwenhywfar DLL Bibliothek
de.DevelFiles=Entwickler-Dateien
de.DocFiles=Dokumentations-Dateien
